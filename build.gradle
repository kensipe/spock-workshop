plugins {
  id 'groovy'
  id 'idea'
  id 'eclipse'
}

def spockVersion = '2.0-groovy-3.0'
def groovyVersion = '3.0.8'

repositories {
    mavenCentral()
	// sonatype repo to get spock snapshots
	maven {
          url "https://oss.sonatype.org/content/groups/public/"
	}
}

dependencies {
    implementation "org.codehaus.groovy:groovy-all:$groovyVersion"
    implementation 'commons-lang:commons-lang:2.6'

    testImplementation "org.spockframework:spock-core:$spockVersion"
    // testCompile "org.spockframework:spock-spring:$spockVersion"  //need if you are doing spring... fails without the spring dependencies

    testImplementation "com.h2database:h2:1.4.197"

    // optional dependencies for using Spock
    testImplementation "org.hamcrest:hamcrest-core:2.2"   // only necessary if Hamcrest matchers are used
    testRuntimeOnly "net.bytebuddy:byte-buddy:1.11.0" // allows mocking of classes (in addition to interfaces)
    testRuntimeOnly "org.objenesis:objenesis:3.2"      // allows mocking of classes without default constructor (together with ByteBuddy or CGLIB)
}

sourceSets {
    main {
        java { srcDirs = [] }
        groovy { srcDir 'src' }
    }
    test {
        java { srcDirs = [] }
        groovy { srcDir 'test' }
    }
}

idea {
    project {
        jdkName = '17'
    }
}

tasks.withType(Test) {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}